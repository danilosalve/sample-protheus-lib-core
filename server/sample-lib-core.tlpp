#include "protheus.ch"

namespace sample.libcore

//------------------------------------------------------------------------------
/*/{Protheus.doc} sampleLibCore
Executa o aplicativo que utiliza o ProtheusLibCore
@type function
@version 1.0
@author TOTVS
@since 09/04/2025
@obs: Para adicionar no menu do Protheus informe o nome do programa adicionado como 
prefixo o namespace: Exemplo - sample.libcore.sampleLibCore
/*/
//------------------------------------------------------------------------------
function sampleLibCore() 
    FwCallApp("sample-protheus-lib-core")
return

//------------------------------------------------------------------------------
/*/{Protheus.doc} jsToAdvpl
Envia os comandos para o frontend de acordo com o tipo da chamada enviada pelo mesmo
@type function
@version 1.0
@author TOTVS
@since 09/04/2025
@param oWebChannel, object, objeto com o socket aberto para comunicação
@param cType, character, tipo/identificador para a chamada
@param cContent, character, conteúdo da chamada
/*/
//------------------------------------------------------------------------------
function jsToAdvpl( oWebChannel as object, cType as character, cContent as character )
	if cType == 'checkBalance'
		oWebChannel:AdvPLToJS('checkBalance', cValToChar(mostrarSaldo(cContent)))
	elseIf cType == 'getParam'
		oWebChannel:AdvPLToJS('setParam', SuperGetMv(cContent))
	endIf
return

//------------------------------------------------------------------------------
/*/{Protheus.doc} MostrarSaldo
Realiza a chamada do cálculo de saldo para determinado produto.
@type function
@version 1.0
@author TOTVS
@since 09/04/2025
@param cProduto, character, código produto
@return numeric, Saldo do produto
/*/
//------------------------------------------------------------------------------
function mostrarSaldo(cProduto as character) as numeric
	local aArea := FwGetArea()
	local aAreaSB2 := SB2->(FwGetArea())
	local nSaldo as numeric

	nSaldo := 0

	SB2->(dbSetOrder(1))
	if SB2->(dbSeek(xFilial("SB2") + cProduto))
		nSaldo := SaldoSB2()
	endIf

	FwRestArea(aAreaSB2)
	FwRestArea(aArea)

	aSize(aArea, 0)
	aSize(aAreaSB2, 0)
return nSaldo
